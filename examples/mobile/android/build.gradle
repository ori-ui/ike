plugins {
    id 'com.android.application'
}

def rustProjectDir = file('..')

android {
    namespace "org.ori.example"
    compileSdk 35

    defaultConfig {
        applicationId "org.ori.example"
        minSdk 21
        targetSdk 35
        ndk {
            abiFilters 'arm64-v8a', 'x86_64'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            jniLibs.srcDirs = ["$buildDir/jniLibs"]
        }
    }
}

def rustTargets = [
    'arm64-v8a': 'aarch64-linux-android',
    'x86_64': 'x86_64-linux-android',
]

tasks.register('run', Exec) {
    group = 'help'
    description = "Installs and runs the Debug build."
    dependsOn 'installDebug'

    def appId = android.defaultConfig.applicationId
    def activity = "$appId/org.ori.RustActivity"

    doFirst {
        // clear old logs
        exec {
            commandLine(
                android.adbExecutable,
                'logcat', '-c',
            )
        }

        // launch the activity
        exec {
            commandLine(
                android.adbExecutable,
                'shell',
                'am', 'start',
                '-n', activity,
            )
        }
    }

    commandLine(
        android.adbExecutable,
        'logcat', '*:S',
        '-s', 'rust',
        '-v', 'raw',
    )
}

android.applicationVariants.all { variant ->
    def variantName = variant.name.capitalize()
    def buildType = variant.buildType.name

    def buildTaskNames = []

    rustTargets.each { abi, triple ->
        def taskName = "cargoBuild$variantName${abi.capitalize()}"

        tasks.register(taskName, Exec) {
            group = 'rust'
            description = "Build Rust ($buildType) for [$abi]."

            workingDir rustProjectDir

            commandLine(
                'cargo', 'build', '--lib',
                '--target', triple,
                '--profile', buildType == 'release'
                    ? 'release'
                    : 'dev',
                '--color', 'always',
            )

            def os = org.gradle.internal.os.OperatingSystem.current()
            def host =
                os.isMacOsX() ? "darwin-x86_64" :
                os.isWindows() ? "windows-x86_64" :
                "linux-x86_64"

            def envTriple = triple.toUpperCase().replace('-', '_')
            def llvmDir = "${android.ndkDirectory}/toolchains/llvm/prebuilt/${host}/bin"
            def clang = "$triple${android.defaultConfig.minSdk}-clang"

            environment 'ANDROID_SDK_ROOT', android.sdkDirectory
            environment 'ANDROID_NDK', android.ndkDirectory
            environment "CARGO_TARGET_${envTriple}_LINKER", "$llvmDir/$clang"
            environment "CARGO_TARGET_${envTriple}_AR", "$llvmDir/llvm-ar"
        }

        buildTaskNames << taskName
    }

    def buildTaskName = "cargoBuild$variantName"

    tasks.register(buildTaskName) {
        group = 'rust'
        description = "Build Rust ($buildType) for (all ABIs)."
        dependsOn buildTaskNames
    }

    def copyTaskName = "copyJniLibs$variantName"

    tasks.register(copyTaskName) {
        group = 'rust'
        description = "Copy compiled Rust ($buildType) shared objects for ${variant.name}."
        dependsOn buildTaskName

        doLast {
            def stdout = new ByteArrayOutputStream()

            exec {
                workingDir rustProjectDir
                commandLine(
                    'cargo',
                    'metadata',
                    '--format-version', '1',
                    '--no-deps',
                )
                standardOutput = stdout
            }

            def json = new groovy.json.JsonSlurper().parseText(stdout.toString("UTF-8"))
            def targetDir = json.target_directory

            rustTargets.each { abi, triple ->
                def fromDir = file("$targetDir/$triple/$buildType")
                def toDir = file("$buildDir/jniLibs/${variant.name}/$abi")

                copy {
                    from fromDir
                    include '*.so'
                    into toDir
                }
            }
        }
    }

    variant.preBuildProvider.configure {
        dependsOn copyTaskName
    }
}
